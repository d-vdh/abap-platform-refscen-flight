extension using interface ZDMOi_agencytp
implementation in class ZDMOzz_bp_x_review_r_agencytp unique;

extend behavior for ZDMOAgency
{
  event ZDMOAgencyReviewCreated parameter ZDMOZZ_D_AgencyReviewCreated;

  extend draft determine action Prepare
  {
    validation ZDMOZZ_Review~ZDMOratingInRange;
  }
  association ZDMOZZ_ReviewZAG { create; with draft; }
}

define behavior for ZDMOZZ_R_Agency_ReviewTP alias ZDMOZZ_Review using ZDMOZZ_I_AGENCY_REVIEWTP
with additional save
persistent table ZDMOzz_agn_reva
draft table ZDMOzz_agn_revd
etag master LocalLastChangedAt
lock dependent
authorization dependent
late numbering
{
  update ( features : instance );
  delete ( features : instance );

  field ( readonly ) ReviewID, AgencyID, HelpfulCount, HelpfulTotal, Reviewer;

  action ( authorization : global ) ZDMOreviewWasHelpful    result [1] $self;
  action ( authorization : global ) ZDMOreviewWasNotHelpful result [1] $self;

  validation ZDMOratingInRange on save { create; field Rating; }

  association _Agency { with draft; }
  mapping for ZDMOzz_agn_reva
    {
      AgencyId           = agency_id;
      ReviewId           = review_id;
      Rating             = rating;
      FreeTextComment    = free_text_comment;
      HelpfulCount       = helpful_count;
      HelpfulTotal       = helpful_total;
      Reviewer           = reviewer;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedAt = local_last_changed_at;
    }

}