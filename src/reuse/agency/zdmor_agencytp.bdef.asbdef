managed implementation in class ZDMObp_r_agencytp unique;
strict ( 2 );
with draft;
extensible
{
  with determinations on modify;
  with determinations on save;
  with validations on save;
  with additional save; }

define behavior for ZDMOR_AgencyTP alias ZDMOAgency
persistent table ZDMOagency
draft table ZDMOagency_d query ZDMOR_AgencyDraft
lock master
total etag LastChangedAt
authorization master ( global )
etag master LocalLastChangedAt
late numbering
extensible
{
  create;
  update;
  delete;

  field ( readonly ) AgencyID;
  field ( mandatory ) CountryCode, EMailAddress, Name;

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  // Validations for mandatory fields
  validation ZDMOvalidateEMailAddress on save { create; field EMailAddress; }
  validation ZDMOvalidateCountryCode on save { create; field CountryCode; }
  validation ZDMOvalidateName on save { create; field Name; }

  validation ZDMOvalidateLargeObject on save { create; field Attachment; field MimeType; field Filename; }

  draft determine action Prepare extensible
  {
    validation ZDMOvalidateEMailAddress;
    validation ZDMOvalidateCountryCode;
    validation ZDMOvalidateName;
    validation ZDMOvalidateLargeObject;
  }

  mapping for ZDMOagency
    {
      AgencyId           = agency_id;
      Name               = name;
      Street             = street;
      PostalCode         = postal_code;
      City               = city;
      CountryCode        = country_code;
      PhoneNumber        = phone_number;
      EmailAddress       = email_address;
      WebAddress         = web_address;
      Attachment         = attachment;
      MimeType           = mime_type;
      Filename           = filename;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}